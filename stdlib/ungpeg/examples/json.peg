
:json_val <- S?(object/array/str/number/true/false/null)S? ->               $2
object    <- '{'S?pair (S? ',' S? pair)* S? '}'/'{'S?'}'   ->      (obj $pair)
pair      <- S? str S? ':' S? json_val S?                  -> ($str $json_val)
array     <- '[' S? json_val (S?','S?json_val)* S?']'
           /'['S?']'                                       ->        $json_val
str       <- '"' (!'"' ('\\\\' / '\\"' / any))* '"'        ->             $any
number    <- int frac? exp?                                ->      (num $1 $2)
int       <- d:'-'? (d:nz_digit d:digit+) / d:digit        ->               $d
frac      <- '.' digit+                                    ->        (frac $2)
exp       <- e digit+                                      ->        (e $e $2)
e         <- [eE] ('+' / '-')?                             ->               $2
nz_digit  <- [1-9]
digit     <- [0-9]
true      <- 'true'                                        ->             true
false     <- 'false'                                       ->            false
null      <- 'null'                                        ->             null
S         <- (' '/'\t'/'\n'/'\r')*

